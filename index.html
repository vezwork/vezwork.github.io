<!DOCTYPE html PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN" "http://www.w3.org/TR/html4/loose.dtd">
<html>
<head>
<meta http-equiv="Content-Type" content="text/html; charset=ISO-8859-1">
<script id="shader-fs" type="x-shader/x-fragment">
  precision highp float;

  // Modified Blum Blum Shub pseudo-random number generator.
  vec2 mBBS(vec2 val, float modulus) {
    val = mod(val, modulus); // For numerical consistancy.
    return mod(val * val, modulus);
  }

  // Pregenerated noise texture.
  uniform sampler2D nzw;
  const float modulus = 61.0;  // Value used in pregenerated noise texture.

  /**
   * Modified noise function.
   * @see http://www.csee.umbc.edu/~olano/papers/index.html#mNoise
   **/
  float mnoise(vec3 pos) {
    float intArg = floor(pos.z);
    float fracArg = fract(pos.z);
    vec2 hash = mBBS(intArg * 3.0 + vec2(0, 3), modulus);
    vec4 g = vec4(
        texture2D(nzw, vec2(pos.x, pos.y + hash.x) / modulus).xy,
        texture2D(nzw, vec2(pos.x, pos.y + hash.y) / modulus).xy) * 2.0 - 1.0;
    return mix(g.x + g.y * fracArg,
               g.z + g.w * (fracArg - 1.0),
               smoothstep(0.0, 1.0, fracArg));
  }

  const int octives = 4;
  const float lacunarity = 2.0;
  const float gain = 0.5;

  /**
   * Adds multiple octives of noise together.
   **/
  float turbulence(vec3 pos) {
    float sum = 0.0;
    float freq = 1.0;
    float amp = 1.0;
    for(int i = 0; i < 4; i++) {
      sum += abs(mnoise(pos * freq)) * amp;
      freq *= lacunarity;
      amp *= gain;
    }
    return sum;
  }

  const float magnatude = 1.3;
  uniform float time;
  uniform float audio;
  uniform float pinch;
  uniform sampler2D fireProfile;

  /**
   * Samples the fire.
   *
   * @param loc the normalized location (0.0-1.0) to sample the fire
   * @param scale the 'size' of the fire in world space and time
   **/
  vec4 sampleFire(vec3 loc, vec4 scale) {
    // Convert xz to [-1.0, 1.0] range.
    loc.xz = loc.xz * 2.0 - 1.0;

    // Convert to (radius, height) to sample fire profile texture.
    vec2 st = vec2(sqrt(dot(loc.xz, loc.xz)), loc.y);

    // Convert loc to 'noise' space
    loc.y -= time * scale.w; // Scrolling noise upwards over time.
    loc *= scale.xyz; // Scaling noise space.

    // Offsetting vertial texture lookup.
    // We scale this by the sqrt of the height so that things are
    // relatively stable at the base of the fire and volital at the
    // top.
    float offset = sqrt(st.y) * magnatude * turbulence(loc);
    st.y += offset*(1.+pinch*15.); //makes it FIERY!!

    if (st.y > 1.0) {
      return vec4(0, 0, 0, 1);
    }

    st.x = st.x*(1.7-audio*2.);
    st.y = st.y*(1.7-audio*2.);

    vec4 result = texture2D(fireProfile, st);
    //vec4 result = vec4(0., 0., loc.x-loc.z*sin(time), 1.);

    //variables to play with:
    //loc: vec3 containing the x, z, and a (useless) y of the position of the fire in the worldCoord
    //time: around 0.18/s
    //pinch: 0-1
    //result: sampling result on the fire texture (rgb)

    //color changing
    result.b = result.g*sin(time*8.);
    result.g = result.g*(cos(time*2.2)*1.5+1.);
    //result.rgb -= pinch/4.;

    // Fading out bottom so slice clipping isnt obvious
    if (st.y < .1) {
      result *= st.y / 0.1;
    }
    return result;
  }

  varying vec3 texOut;
  varying vec3 worldCoord;
  void main(void) {
    // Mapping texture coordinate to -1 => 1 for xy, 0=> 1 for y

    //gl_FragColor = vec4(0.5, 0.5, 0.0, 0.5);
    //gl_FragColor = vec4(texOut, 1);
    //gl_FragColor = vec4(vec3((t.x - t.y) / 2.0), 1);
    vec3 color = sampleFire(texOut, vec4(1.0, 2.0, 1.0, 0.5)).xyz;
    //gl_FragColor = vec4(color, length(color) * 2.5);
    gl_FragColor = vec4(color * .25, 1);
  }
</script>

<script id="shader-vs" type="x-shader/x-vertex">
  attribute vec3 pos;
  attribute vec3 tex;
  uniform mat4 projectionMatrix;
  uniform mat4 modelViewMatrix;

  varying vec3 texOut;

  void main(void) {
    gl_Position = projectionMatrix * modelViewMatrix * vec4(pos, 1);
    texOut = tex;
  }
</script>

<script type="text/javascript" src="/scripts/glMatrix.js"></script>
<script type="text/javascript" src="/scripts/priority_queue.js"></script>
<script type="text/javascript" src="/scripts/gl_context.js"></script>
<script type="text/javascript" src="/scripts/scene_graph.js"></script>
<script type="text/javascript" src="/scripts/ctrl.js"></script>
<script type="text/javascript" src="/scripts/volume-meter.js"></script>
<script type="text/javascript" src="/scripts/leap-0.6.4.min.js"></script>
<script type="text/javascript">

  var test = 'result.rgb -= pinch/4.;'
  var bubby = ' '
  //templateHTML(document.getElementById('shader-fs'))



  var pinch = 0;
  var audio;

  Leap.loop({enableGestures: false}, function(frame) {
    if (frame.hands.length > 0) {
      var handVector = frame.hands[0].palmPosition
      pinch = frame.hands[0].pinchStrength

      //allow leap control of 3d position and rotation of fire
      //document.getElementById("fire_canvas").mover(handVector[0]/10, 0 ,15-handVector[1]/10);
      //document.getElementById("fire_canvas2").mover(handVector[0]/10, 0 ,15-handVector[1]/10);
      //document.getElementById("fire_canvas3").mover(handVector[0]/10, 0 ,15-handVector[1]/10);
      //document.getElementById("fire_canvas4").mover(handVector[0]/10, 0 ,15-handVector[1]/10);
    } else {
      pinch = 0
    }
  });

  var uniforms = [{ //time is automatically added as a uniform
    name: 'audio',
    value: 0.0,
    type: '1f'
  }, {
    name: 'pinch',
    value: 0.0,
    type: '1f'
  }]

  var options = {
    uniforms: uniforms,
    timeRate: 5000.0,                       //lower mumber = faster fire
    clearColor: [0.0, 0.0, 0.0, 0.0],
    camAngle: 45,
    camPos: [0.0, 0.5, 5],
    canvasSize: {height: 350, width: 350},
    texPath: "/textures/firetex2.png"
  }

  function updateUniforms() {

    //uniforms[0].value = audio.volume;
    uniforms[0].value -= (uniforms[0].value - audio.volume)*0.2;
    uniforms[1].value = pinch;

    moveFire(2.5,0,0); //fire constantly spins


    window.requestAnimationFrame(updateUniforms);
  }

  window.onload = function() {

    document.body.addEventListener("click",function() {
      console.log(document.body);
      document.body.webkitRequestFullscreen();

    });

    initAudio().then((result)=> {

      audio = result;
      updateUniforms();

      initFire("fire_canvas", options);
      initFire("fire_canvas2", options);
      initFire("fire_canvas3", options);
      initFire("fire_canvas4", options);

    }).catch(function(e) {
      console.log(e);

    });

  }
</script>
<title>Procedural Volumetric Fire</title>
<style>
body {
  margin: 0;
  background: black;
}

canvas {
  position: absolute;
}
</style>
</head>
<body>
  <canvas id="fire_canvas" style="border: none;" onclick="mover(10,0,1)"></canvas>
  <canvas id="fire_canvas2" style="border: none;"></canvas>
  <canvas id="fire_canvas3" style="border: none;"></canvas>
  <canvas id="fire_canvas4" style="border: none;"></canvas>
</body>
</html>
